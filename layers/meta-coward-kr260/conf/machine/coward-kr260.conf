#@TYPE: Machine
#@NAME: Specific Kria KR260 Robotics Starter Kit of mine
#@DESCRIPTION: Machine configuration for Kria KR260 Robotics Starter Kit (Kria K26 SOM + KR260 carrier board).

#### Preamble
MACHINEOVERRIDES =. "${@['', 'coward-kr260:']['coward-kr260' != '${MACHINE}']}"
MACHINEOVERRIDES =. "k26-kr:"
#### Regular settings follow

require conf/machine/include/k26-smk.inc
require conf/machine/include/kria-qemu-kr.inc

INITRAMFS_IMAGE ?= "petalinux-initramfs-image"

# This is an 'XSCT' based BSP
XILINX_XSCT_VERSION = "2024.2"
XILINX_WITH_ESW = "xsct"

COWARD_XSA_PATH ?= "file://kr260_plane_jane.xsa"
COWARD_XSA_SHA256 ?= "b36db3b3856502c3b275cafb58f53bdd591c2112c135dc2c0b9d3e8171406ecc"

# Add system XSA
HDF_URI = "${COWARD_XSA_PATH}"
HDF_URI[sha256sum] = "${COWARD_XSA_SHA256}"

UBOOT_DT_FILES = "\
    zynqmp-sck-kr-g-revA.dtso \
    zynqmp-sck-kr-g-revB.dtso \
    "

# DTB_FILE_NAME cant be changed globally or it will break the build but we can modify it for these
# tasks specifically so that the resulting symlink points to the correct file for a specific board
DTB_FILE_NAME:task-install:pn-device-tree = "${UBOOT_DTFILE_PREFIX}-zynqmp-sck-kr-g-revB.dtb"
DTB_FILE_NAME:task-deploy:pn-device-tree = "${UBOOT_DTFILE_PREFIX}-zynqmp-sck-kr-g-revB.dtb"
# Workaround for bitbake not detecting the above change in the hash
PR:append:pn-device-tree = ".1"

# Use custom name for xilinx-bootbin, because it is removed for kria soms in a xilinx inc.
# also add virtual/bitstream to the image depends so that is extracted as well
EXTRA_IMAGEDEPENDS += "virtual/bootbin virtual/bitstream"

# Kernel Configuration
PREFERRED_PROVIDER_virtual/kernel = "linux-xlnx"

#### No additional settings should be after the Postamble
#### Postamble
PACKAGE_EXTRA_ARCHS:append = "${@['', ' coward_kr260']['coward-kr260' != "${MACHINE}"]}"